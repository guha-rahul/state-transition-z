const std = @import("std");
const ForkSeq = @import("config").ForkSeq;
const ForkRunner = @import("../runner/fork.zig");

pub const handlers = std.enums.values(ForkRunner.Handler);
pub const header =
    \\// This file is generated by write_spec_tests.zig.
    \\// Do not commit changes by hand.
    \\
    \\const std = @import("std");
    \\const ForkSeq = @import("config").ForkSeq;
    \\const active_preset = @import("preset").active_preset;
    \\const spec_test_options = @import("spec_test_options");
    \\const ForkRunner = @import("../runner/fork.zig");
    \\
    \\const allocator = std.testing.allocator;
    \\
    \\
;

const test_template =
    \\test "{s} fork {s}" {{
    \\    const test_dir_name = try std.fs.path.join(allocator, &[_][]const u8{{
    \\        spec_test_options.spec_test_out_dir,
    \\        spec_test_options.spec_test_version,
    \\        @tagName(active_preset) ++ "/tests/" ++ @tagName(active_preset) ++ "/{s}/fork/{s}/{s}",
    \\    }});
    \\    defer allocator.free(test_dir_name);
    \\    const test_dir = std.fs.cwd().openDir(test_dir_name, .{{}}) catch return error.SkipZigTest;
    \\
    \\    try ForkRunner.TestCase(.{s}).execute(allocator, test_dir);
    \\}}
    \\
    \\
;

pub fn writeHeader(writer: std.io.AnyWriter) !void {
    try writer.print(header, .{});
}

pub fn writeTest(
    writer: std.io.AnyWriter,
    fork: ForkSeq,
    comptime handler: ForkRunner.Handler,
    test_case_name: []const u8,
) !void {
    const suite = handler.suiteName();
    try writer.print(test_template, .{
        @tagName(fork),
        test_case_name,
        @tagName(fork),
        suite,
        test_case_name,
        @tagName(fork),
    });
}
