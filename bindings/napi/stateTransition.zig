const std = @import("std");
const zapi = @import("zapi:zapi");
const js = zapi.js;
const napi = zapi.napi;
const builtin = @import("builtin");
const fork_types = @import("fork_types");
const st = @import("state_transition");
const CachedBeaconState = st.CachedBeaconState;
const napi_io = @import("./io.zig");
const AnySignedBeaconBlock = fork_types.AnySignedBeaconBlock;

var gpa: std.heap.DebugAllocator(.{}) = .init;
const allocator = if (builtin.mode == .Debug)
    gpa.allocator()
else
    std.heap.c_allocator;

/// Parse a JS options object into Zig's TransitionOpts.
///
/// Recognized fields:
/// - verifyStateRoot, verifyProposer, verifySignatures: bool
/// - dontTransferCache: bool (negated to set transfer_cache)
/// - executionPayloadStatus: "valid" | "invalid" | "preMerge"
/// - dataAvailabilityStatus: "Available" | "PreData" | "OutOfRange"
///
/// This is the double negative version to conform with production lodestar.
/// TODO(bing): Eventually rename this to `transferCache` to avoid double negation because its confusing naming.
fn parseOptions(options: ?js.Value) !st.TransitionOpts {
    var transition_opts: st.TransitionOpts = .{};
    if (options) |value| {
        const raw = value.toValue();
        if (try raw.typeof() == .object) {
            if (try raw.hasNamedProperty("verifyStateRoot")) {
                transition_opts.verify_state_root = try (try raw.getNamedProperty("verifyStateRoot")).getValueBool();
            }
            if (try raw.hasNamedProperty("verifyProposer")) {
                transition_opts.verify_proposer = try (try raw.getNamedProperty("verifyProposer")).getValueBool();
            }
            if (try raw.hasNamedProperty("verifySignatures")) {
                transition_opts.verify_signatures = try (try raw.getNamedProperty("verifySignatures")).getValueBool();
            }
            if (try raw.hasNamedProperty("dontTransferCache")) {
                transition_opts.transfer_cache = !(try (try raw.getNamedProperty("dontTransferCache")).getValueBool());
            }
            if (try raw.hasNamedProperty("executionPayloadStatus")) {
                var buf: [16]u8 = undefined;
                const execution_payload_status = try (try raw.getNamedProperty("executionPayloadStatus")).getValueStringUtf8(&buf);
                transition_opts.block_external_data.execution_payload_status =
                    if (std.mem.eql(u8, execution_payload_status, "valid"))
                        .valid
                    else if (std.mem.eql(u8, execution_payload_status, "invalid"))
                        .invalid
                    else if (std.mem.eql(u8, execution_payload_status, "preMerge"))
                        .pre_merge
                    else
                        return error.InvalidExecutionPayloadStatus;
            }
            if (try raw.hasNamedProperty("dataAvailabilityStatus")) {
                var buf: [16]u8 = undefined;
                const da_status = try (try raw.getNamedProperty("dataAvailabilityStatus")).getValueStringUtf8(&buf);
                transition_opts.block_external_data.data_availability_status =
                    if (std.mem.eql(u8, da_status, "Available"))
                        .available
                    else if (std.mem.eql(u8, da_status, "PreData"))
                        .pre_data
                    else if (std.mem.eql(u8, da_status, "OutOfRange"))
                        .out_of_range
                        // TODO(bing): uncomment once gloas support is in
                        // else if (std.mem.eql(u8, da_status, "NotRequired")) .not_required;
                    else
                        return error.InvalidDataAvailabilityStatus;
            }
        }
    }
    return transition_opts;
}

/// Perform a state transition given a signed beacon block.
///
/// Arguments:
/// - arg 0: BeaconStateView instance (the pre-state)
/// - arg 1: signed block bytes (Uint8Array)
/// - arg 2: options object (optional) with:
///   - verifyStateRoot: bool (default true)
///   - verifyProposer: bool (default true)
///   - verifySignatures: bool (default false)
///   - transferCache: bool (default true)
/// Returns: BeaconStateView (the post-state)
pub fn stateTransition(
    pre_state_value: js.Value,
    signed_block_bytes: js.Uint8Array,
    options: ?js.Value,
) !js.Value {
    const env = js.env();
    const pre_state = pre_state_value.toValue();
    const cached_state = try env.unwrap(CachedBeaconState, pre_state);
    const transition_opts = try parseOptions(options);
    const signed_block_bytes_slice = try signed_block_bytes.toSlice();

    const current_epoch = st.computeEpochAtSlot(try cached_state.state.slot());
    const fork = cached_state.config.forkSeqAtEpoch(current_epoch);
    const signed_block = try AnySignedBeaconBlock.deserialize(
        allocator,
        .full,
        fork,
        signed_block_bytes_slice,
    );
    defer signed_block.deinit(allocator);

    const post_state = try st.stateTransition(
        allocator,
        napi_io.get(),
        cached_state,
        signed_block,
        transition_opts,
    );
    errdefer {
        post_state.deinit();
        allocator.destroy(post_state);
    }

    const ctor = try pre_state.getNamedProperty("constructor");
    const new_state_value = try env.newInstance(ctor, .{});
    const dummy_state = try env.unwrap(CachedBeaconState, new_state_value);
    dummy_state.* = post_state.*;
    allocator.destroy(post_state);

    return .{ .val = new_state_value };
}
