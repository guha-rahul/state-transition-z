const std = @import("std");
const Allocator = std.mem.Allocator;
const BeaconConfig = @import("config").BeaconConfig;
const ForkSeq = @import("config").ForkSeq;
const BeaconState = @import("fork_types").BeaconState;
const ct = @import("consensus_types");
const preset = @import("preset").preset;
const c = @import("constants");
const getBlockRootAtSlot = @import("./block_root.zig").getBlockRootAtSlot;
const computeEpochAtSlot = @import("./epoch.zig").computeEpochAtSlot;
const AnyBeaconState = @import("fork_types").AnyBeaconState;
const computePayloadTimelinessCommitteeForSlot = @import("./seed.zig").computePayloadTimelinessCommitteeForSlot;
const computePayloadTimelinessCommitteesForEpoch = @import("./seed.zig").computePayloadTimelinessCommitteesForEpoch;
const getSeed = @import("./seed.zig").getSeed;
const Sha256 = std.crypto.hash.sha2.Sha256;
const EpochShuffling = @import("./epoch_shuffling.zig").EpochShuffling;
const computeEpochShuffling = @import("./epoch_shuffling.zig").computeEpochShuffling;
const isActiveValidator = @import("./validator.zig").isActiveValidator;
const computeStartSlotAtEpoch = @import("./epoch.zig").computeStartSlotAtEpoch;
const RootCache = @import("../cache/root_cache.zig").RootCache;
const EpochCache = @import("../cache/epoch_cache.zig").EpochCache;
const computeDomain = @import("./domain.zig").computeDomain;
const computeSigningRoot = @import("./signing_root.zig").computeSigningRoot;
const bls = @import("bls");
const verify = @import("./bls.zig").verify;

const BLSPubkey = ct.primitive.BLSPubkey.Type;
const ValidatorIndex = ct.primitive.ValidatorIndex.Type;
const ExecutionAddress = ct.primitive.ExecutionAddress.Type;
const BLSSignature = ct.primitive.BLSSignature.Type;

pub fn isBuilderWithdrawalCredential(withdrawal_credentials: *const [32]u8) bool {
    return withdrawal_credentials[0] == c.BUILDER_WITHDRAWAL_PREFIX;
}

pub fn getBuilderPaymentQuorumThreshold(epoch_cache: *const EpochCache) u64 {
    const quorum = (epoch_cache.total_active_balance_increments * preset.EFFECTIVE_BALANCE_INCREMENT / preset.SLOTS_PER_EPOCH) *
        c.BUILDER_PAYMENT_THRESHOLD_NUMERATOR;
    return quorum / c.BUILDER_PAYMENT_THRESHOLD_DENOMINATOR;
}

fn hasBuilderIndexFlag(index: u64) bool {
    return (index & c.BUILDER_INDEX_FLAG) != 0;
}

/// Check if a validator index represents a builder (has the builder flag set).
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#new-is_builder_index
pub fn isBuilderIndex(validator_index: u64) bool {
    return hasBuilderIndexFlag(validator_index);
}

/// Convert a builder index to a flagged validator index for use in Withdrawal containers.
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#new-convert_builder_index_to_validator_index
pub fn convertBuilderIndexToValidatorIndex(builder_index: u64) u64 {
    return if (hasBuilderIndexFlag(builder_index)) builder_index else builder_index | c.BUILDER_INDEX_FLAG;
}

/// Convert a flagged validator index back to a builder index.
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#new-convert_validator_index_to_builder_index
pub fn convertValidatorIndexToBuilderIndex(validator_index: u64) u64 {
    return if (hasBuilderIndexFlag(validator_index)) validator_index & ~c.BUILDER_INDEX_FLAG else validator_index;
}

/// Check if a builder is active (deposited and not yet withdrawable).
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#isactivebuilder
pub fn isActiveBuilder(builder: *const ct.gloas.Builder.Type, finalized_epoch: u64) bool {
    return builder.deposit_epoch < finalized_epoch and builder.withdrawable_epoch == c.FAR_FUTURE_EPOCH;
}

/// Compute the gas limit that satisfies the EIP-1559 adjustment rule from `parent_gas_limit`,
/// clamping `target_gas_limit` into the allowed window of `±max(parent_gas_limit / 1024, 1) - 1`.
///
/// From https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1559.md
pub fn getExpectedGasLimit(parent_gas_limit: u64, target_gas_limit: u64) u64 {
    const max_gas_limit_difference = @max(parent_gas_limit / 1024, 1) - 1;

    if (target_gas_limit > parent_gas_limit) {
        return parent_gas_limit + @min(target_gas_limit - parent_gas_limit, max_gas_limit_difference);
    }

    return parent_gas_limit - @min(parent_gas_limit - target_gas_limit, max_gas_limit_difference);
}

/// Check if `gas_limit` is compatible with `target_gas_limit` under the EIP-1559 transition rule
/// from `parent_gas_limit`. The bid must hit `target_gas_limit` when the target is within one
/// adjustment step of the parent, otherwise it must hit the clamped boundary.
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.8/specs/gloas/builder.md#new-is_gas_limit_target_compatible
pub fn isGasLimitTargetCompatible(parent_gas_limit: u64, gas_limit: u64, target_gas_limit: u64) bool {
    return gas_limit == getExpectedGasLimit(parent_gas_limit, target_gas_limit);
}

/// Get the total pending balance to withdraw for a builder (from withdrawals + payments).
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#new-get_pending_balance_to_withdraw_for_builder
pub fn getPendingBalanceToWithdrawForBuilder(allocator: Allocator, state: *BeaconState(.gloas), builder_index: u64) !u64 {
    var pending_balance: u64 = 0;

    var withdrawals = try state.inner.getReadonly("builder_pending_withdrawals");
    const withdrawals_len = try withdrawals.length();
    var w_it = withdrawals.iteratorReadonly(0);
    for (0..withdrawals_len) |_| {
        const w = try w_it.nextValue();
        if (w.builder_index == builder_index) {
            pending_balance += w.amount;
        }
    }

    var payments = try state.inner.getReadonly("builder_pending_payments");
    const payments_len = ct.gloas.BeaconState.getFieldType("builder_pending_payments").length;
    for (0..payments_len) |i| {
        var p: ct.gloas.BuilderPendingPayment.Type = undefined;
        try payments.getValue(allocator, i, &p);
        if (p.withdrawal.builder_index == builder_index) {
            pending_balance += p.withdrawal.amount;
        }
    }

    return pending_balance;
}

/// Check if a builder has sufficient balance to cover a bid amount.
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#new-can_builder_cover_bid
pub fn canBuilderCoverBid(allocator: Allocator, state: *BeaconState(.gloas), builder_index: u64, bid_amount: u64) !bool {
    var builders = try state.inner.getReadonly("builders");
    var builder: ct.gloas.Builder.Type = undefined;
    try builders.getValue(allocator, builder_index, &builder);

    const pending_balance = try getPendingBalanceToWithdrawForBuilder(allocator, state, builder_index);
    const min_balance = preset.MIN_DEPOSIT_AMOUNT + pending_balance;

    if (builder.balance < min_balance) return false;
    return builder.balance - min_balance >= bid_amount;
}

/// Initiate a builder exit by setting their withdrawable epoch.
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.1/specs/gloas/beacon-chain.md#new-initiate_builder_exit
pub fn initiateBuilderExit(config: *const BeaconConfig, state: *BeaconState(.gloas), allocator: Allocator, builder_index: u64) !void {
    var builders = try state.inner.get("builders");
    var builder: ct.gloas.Builder.Type = undefined;
    try builders.getValue(allocator, builder_index, &builder);

    if (builder.withdrawable_epoch != c.FAR_FUTURE_EPOCH) return;

    const current_epoch = computeEpochAtSlot(try state.slot());
    builder.withdrawable_epoch = current_epoch + config.chain.MIN_BUILDER_WITHDRAWABILITY_DELAY;
    try builders.setValue(builder_index, &builder);
}

/// Find the index of a builder by their public key.
/// Returns null if not found.
///
/// May consider builder pubkey cache if performance becomes an issue.
pub fn findBuilderIndexByPubkey(allocator: Allocator, state: *BeaconState(.gloas), pubkey: *const BLSPubkey) !?usize {
    var builders = try state.inner.get("builders");
    const len = try builders.length();
    for (0..len) |i| {
        var b: ct.gloas.Builder.Type = undefined;
        try builders.getValue(allocator, i, &b);
        if (std.mem.eql(u8, &b.pubkey, pubkey)) return i;
    }
    return null;
}

/// Add a new builder to the builders registry. Reuses slots from exited and fully withdrawn
/// builders when available, otherwise appends.
///
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.11/specs/gloas/beacon-chain.md#new-add_builder_to_registry
pub fn addBuilderToRegistry(
    allocator: Allocator,
    state: *BeaconState(.gloas),
    pubkey: *const BLSPubkey,
    version: u8,
    execution_address: *const ExecutionAddress,
    amount: u64,
    slot: u64,
) !void {
    var builders = try state.inner.get("builders");
    const len = try builders.length();
    const current_epoch = computeEpochAtSlot(try state.slot());

    var builder_index: ?usize = null;
    for (0..len) |i| {
        var builder: ct.gloas.Builder.Type = undefined;
        try builders.getValue(allocator, i, &builder);
        if (builder.withdrawable_epoch <= current_epoch and builder.balance == 0) {
            builder_index = i;
            break;
        }
    }

    const new_builder = ct.gloas.Builder.Type{
        .pubkey = pubkey.*,
        .version = version,
        .execution_address = execution_address.*,
        .balance = amount,
        .deposit_epoch = computeEpochAtSlot(slot),
        .withdrawable_epoch = c.FAR_FUTURE_EPOCH,
    };

    if (builder_index) |idx| {
        try builders.setValue(idx, &new_builder);
    } else {
        try builders.pushValue(&new_builder);
        try state.inner.set("builders", builders);
    }
}

/// Verify the proof of possession on a builder deposit request.
///
/// The dedicated `DOMAIN_BUILDER_DEPOSIT` (vs the validator `DOMAIN_DEPOSIT`) prevents replay
/// of validator deposit signatures against the builder deposit contract and vice versa.
///
/// Spec: https://github.com/ethereum/consensus-specs/blob/v1.7.0-alpha.11/specs/gloas/beacon-chain.md#new-is_valid_builder_deposit_signature
pub fn isValidBuilderDepositSignature(
    config: *const BeaconConfig,
    pubkey: *const BLSPubkey,
    withdrawal_credentials: *const [32]u8,
    amount: u64,
    deposit_signature: BLSSignature,
) bool {
    const deposit_message = ct.phase0.DepositMessage.Type{
        .pubkey = pubkey.*,
        .withdrawal_credentials = withdrawal_credentials.*,
        .amount = amount,
    };

    var domain: ct.primitive.Domain.Type = undefined;
    computeDomain(c.DOMAIN_BUILDER_DEPOSIT, config.chain.GENESIS_FORK_VERSION, c.ZERO_HASH, &domain) catch return false;

    var signing_root: [32]u8 = undefined;
    computeSigningRoot(ct.phase0.DepositMessage, &deposit_message, &domain, &signing_root) catch return false;

    const public_key = bls.PublicKey.uncompress(pubkey) catch return false;
    public_key.validate() catch return false;
    const signature = bls.Signature.uncompress(&deposit_signature) catch return false;
    signature.validate(true) catch return false;
    verify(&signing_root, &public_key, &signature, .{}) catch return false;
    return true;
}

pub fn isAttestationSameSlot(state: *BeaconState(.gloas), data: *const ct.phase0.AttestationData.Type) !bool {
    if (data.slot == 0) return true;

    const block_root = try getBlockRootAtSlot(.gloas, state, data.slot);
    const is_matching = std.mem.eql(u8, &data.beacon_block_root, block_root);

    const prev_block_root = try getBlockRootAtSlot(.gloas, state, data.slot - 1);
    const is_current = !std.mem.eql(u8, &data.beacon_block_root, prev_block_root);

    return is_matching and is_current;
}

pub fn isAttestationSameSlotRootCache(root_cache: *RootCache(.gloas), data: *const ct.phase0.AttestationData.Type) !bool {
    if (data.slot == 0) return true;

    const block_root = try root_cache.getBlockRootAtSlot(data.slot);
    const is_matching = std.mem.eql(u8, &data.beacon_block_root, block_root);

    const prev_block_root = try root_cache.getBlockRootAtSlot(data.slot - 1);
    const is_current = !std.mem.eql(u8, &data.beacon_block_root, prev_block_root);

    return is_matching and is_current;
}

pub fn isParentBlockFull(state: *BeaconState(.gloas)) !bool {
    var bid = try state.inner.getReadonly("latest_execution_payload_bid");
    const bid_block_hash = try bid.getFieldRoot("block_hash");
    const latest_block_hash = try state.inner.getFieldRoot("latest_block_hash");
    return std.mem.eql(u8, bid_block_hash, latest_block_hash);
}

pub fn computePtc(
    allocator: Allocator,
    state: *BeaconState(.gloas),
    slot: u64,
    shuffling: ?*EpochShuffling,
    effective_balance_increments: []const u16,
) ![ct.gloas.PtcWindow.Element.length]ValidatorIndex {
    const epoch = computeEpochAtSlot(slot);

    const epoch_shuffling = if (shuffling) |s| s else blk: {
        var validators = try state.validators();
        const validator_count = try validators.length();
        var active_indices: std.ArrayList(ValidatorIndex) = .empty;
        defer active_indices.deinit(allocator);
        for (0..validator_count) |i| {
            var validator: ct.phase0.Validator.Type = undefined;
            try validators.getValue(undefined, i, &validator);
            if (isActiveValidator(&validator, epoch)) {
                try active_indices.append(allocator, @intCast(i));
            }
        }
        var any_state = AnyBeaconState{ .gloas = state.inner };
        break :blk try computeEpochShuffling(allocator, &any_state, try active_indices.toOwnedSlice(allocator), epoch);
    };
    defer if (shuffling == null) epoch_shuffling.deinit();

    var epoch_seed: [32]u8 = undefined;
    try getSeed(.gloas, state, epoch, c.DOMAIN_PTC_ATTESTER, &epoch_seed);

    var slot_seed_input: [40]u8 = undefined;
    @memcpy(slot_seed_input[0..32], &epoch_seed);
    std.mem.writeInt(u64, slot_seed_input[32..][0..8], slot, .little);

    var slot_seed: [32]u8 = undefined;
    Sha256.hash(&slot_seed_input, &slot_seed, .{});

    const slot_committees = epoch_shuffling.committees[slot % preset.SLOTS_PER_EPOCH];
    return computePayloadTimelinessCommitteeForSlot(allocator, &slot_seed, slot_committees, effective_balance_increments);
}

pub fn initializePtcWindow(
    comptime fork: ForkSeq,
    allocator: Allocator,
    epoch_cache: *const EpochCache,
    state: *BeaconState(fork),
) ![ct.gloas.PtcWindow.length][ct.gloas.PtcWindow.Element.length]ValidatorIndex {
    const ptc_size = ct.gloas.PtcWindow.Element.length;
    const ptc_window_len = ct.gloas.PtcWindow.length;

    const empty_previous_epoch = [_][ptc_size]ValidatorIndex{
        [_]ValidatorIndex{0} ** ptc_size,
    } ** preset.SLOTS_PER_EPOCH;

    const current_epoch = computeEpochAtSlot(try state.slot());
    var ptcs: [(1 + preset.MIN_SEED_LOOKAHEAD) * preset.SLOTS_PER_EPOCH][ptc_size]ValidatorIndex = undefined;
    for (0..1 + preset.MIN_SEED_LOOKAHEAD) |epoch_offset| {
        const epoch = current_epoch + epoch_offset;
        const epoch_committees = try computePayloadTimelinessCommitteesForEpoch(
            fork,
            allocator,
            state,
            epoch,
            epoch_cache,
        );
        for (0..preset.SLOTS_PER_EPOCH) |slot_index| {
            ptcs[epoch_offset * preset.SLOTS_PER_EPOCH + slot_index] = epoch_committees[slot_index];
        }
    }

    // return empty_previous_epoch + ptcs
    var ptc_window: [ptc_window_len][ptc_size]ValidatorIndex = undefined;
    @memcpy(ptc_window[0..preset.SLOTS_PER_EPOCH], &empty_previous_epoch);
    @memcpy(ptc_window[preset.SLOTS_PER_EPOCH..], &ptcs);

    return ptc_window;
}
