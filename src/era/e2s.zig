const std = @import("std");
const preset = @import("preset").preset;

/// Maximum allowed size for an entry data payload in an E2Store (.e2s) file.
/// Arbitrary limit to prevent excessive memory usage
pub const max_entry_data_size = 512 * 1024 * 1024; // 512 MiB

/// Maximum allowed number of offsets in a SlotIndex entry.
/// Arbitrary limit to prevent excessive memory usage
pub const max_slot_index_count = preset.SLOTS_PER_HISTORICAL_ROOT * 10;

/// Known entry types in an E2Store (.e2s) file along with their exact 2-byte codes.
pub const EntryType = enum(u16) {
    Empty = 0,
    CompressedSignedBeaconBlock = 1,
    CompressedBeaconState = 2,
    Version = 0x65 | (0x32 << 8), // "e2" in ASCII
    SlotIndex = 0x69 | (0x32 << 8), // "i2" in ASCII

    pub fn fromU16(bytes: u16) error{UnknownEntryType}!EntryType {
        inline for (std.meta.fields(EntryType)) |field| {
            if (bytes == @intFromEnum(@field(EntryType, field.name))) {
                return @field(EntryType, field.name);
            }
        }
        return error.UnknownEntryType;
    }

    pub fn toU16(self: EntryType) u16 {
        return @intFromEnum(self);
    }
};

pub const ReadError = error{
    UnknownEntryType,
    UnexpectedEntryType,
    UnexpectedEOF,
    InvalidVersionHeader,
    InvalidSlotIndexCount,
    InvalidHeaderReservedBytes,
    Overflow,
    DataSizeTooLarge,
} || std.fs.File.PReadError || std.mem.Allocator.Error;

/// Parsed entry from an E2Store (.e2s) file.
pub const Entry = struct {
    entry_type: EntryType,
    data: []const u8,
};

pub const SlotIndex = struct {
    /// First slot covered by this index (era * SLOTS_PER_HISTORICAL_ROOT)
    start_slot: u64,
    /// File positions where data can be found. Length varies by index type.
    offsets: []i64,
    /// File position where this index record starts
    record_start: u64,

    /// Serialize a SlotIndex into a byte array.
    ///
    /// Ownership of the returned byte array is transferred to the caller.
    pub fn serialize(self: SlotIndex, allocator: std.mem.Allocator) std.mem.Allocator.Error![]u8 {
        const count = self.offsets.len;
        const size = count * 8 + 16;

        const payload = try allocator.alloc(u8, size);
        errdefer allocator.free(payload);

        // Write start slot
        std.mem.writeInt(u64, payload[0..8], self.start_slot, .little);

        // Write offsets
        @memcpy(std.mem.bytesAsSlice(i64, payload[8 .. size - 8]), self.offsets);

        // Write count
        std.mem.writeInt(u64, payload[size - 8 ..][0..8], @intCast(count), .little);

        return payload;
    }

    pub fn deinit(self: SlotIndex, allocator: std.mem.Allocator) void {
        allocator.free(self.offsets);
    }
};

/// The complete version record.
pub const version_record_bytes = [8]u8{ 0x65, 0x32, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00 };

pub const header_size = 8;

/// Read an entry at a specific offset from an open file handle.
/// Reads the header first to determine data length, then reads the complete entry.
pub fn readEntry(allocator: std.mem.Allocator, file: std.fs.File, offset: u64) ReadError!Entry {
    // Read header
    var header: [8]u8 = undefined;
    const header_read_size = try file.pread(&header, offset);
    if (header_read_size != header_size) {
        return error.UnexpectedEOF;
    }

    // Validate entry type from first 2 bytes (little endian)
    const entry_type = try EntryType.fromU16(std.mem.readInt(u16, header[0..2], .little));

    // Parse data length from next 4 bytes (little endian)
    const data_len = std.mem.readInt(u32, header[2..6], .little);

    if (data_len > max_entry_data_size) {
        return error.DataSizeTooLarge;
    }

    // Validate reserved bytes are zero (offset 6-7)
    if (header[6] != 0 or header[7] != 0) {
        return error.InvalidHeaderReservedBytes;
    }

    // Read entry payload/data
    const data = try allocator.alloc(u8, data_len);
    errdefer allocator.free(data);

    const data_read_size = try file.pread(data, offset + header_size);
    if (data_read_size != data_len) {
        return error.UnexpectedEOF;
    }

    return .{
        .entry_type = entry_type,
        .data = data,
    };
}

pub fn readVersion(file: std.fs.File, offset: u64) ReadError!void {
    var header: [8]u8 = undefined;
    const header_read_size = try file.pread(&header, offset);
    if (header_read_size != header_size) {
        return error.UnexpectedEOF;
    }
    if (!std.mem.eql(u8, &header, &version_record_bytes)) {
        return error.InvalidVersionHeader;
    }
}

/// Read a SlotIndex entry at a specific offset from an open file handle.
///
/// Ownership of the returned SlotIndex is transferred to the caller.
pub fn readSlotIndex(allocator: std.mem.Allocator, file: std.fs.File, offset: u64) ReadError!SlotIndex {
    const record_end = offset;
    var count_buffer: [8]u8 = undefined;
    const count_read_size = try file.pread(&count_buffer, record_end - 8);
    if (count_read_size != header_size) {
        return error.UnexpectedEOF;
    }
    const count = std.mem.readInt(u64, count_buffer[0..8], .little);

    if (count > max_slot_index_count) {
        return error.InvalidSlotIndexCount;
    }

    // Validate index position is within file bounds
    const record_start = try std.math.sub(u64, record_end, (8 * count + 24));

    const entry = try readEntry(allocator, file, record_start);
    defer allocator.free(entry.data);

    if (entry.entry_type != EntryType.SlotIndex) {
        return error.UnexpectedEntryType;
    }

    // Size: start_slot(8) + offsets(count*8) + count(8) = count*8 + 16
    const expected_size = count * 8 + 16;
    if (entry.data.len != expected_size) {
        return error.InvalidSlotIndexCount;
    }

    // Parse start slot from payload
    const start_slot = std.mem.readInt(u64, entry.data[0..8], .little);

    // Parse offsets from payload
    const offsets = try allocator.alloc(i64, count);
    errdefer allocator.free(offsets);

    @memcpy(offsets, std.mem.bytesAsSlice(i64, entry.data[8 .. entry.data.len - 8]));

    return .{
        .start_slot = start_slot,
        .offsets = offsets,
        .record_start = record_start,
    };
}

pub const WriteError = error{} || std.fs.File.PWriteError;

pub fn writeEntry(file: std.fs.File, offset: u64, entry_type: EntryType, payload: []const u8) WriteError!void {
    var header: [8]u8 = [_]u8{0} ** 8;
    std.mem.writeInt(u16, header[0..2], entry_type.toU16(), .little);
    std.mem.writeInt(u32, header[2..6], @intCast(payload.len), .little);
    try file.pwriteAll(&header, offset);
    try file.pwriteAll(payload, offset + header_size);
}

pub fn writeVersion(file: std.fs.File, offset: u64) WriteError!void {
    try file.pwriteAll(&version_record_bytes, offset);
}

// ── Unit tests ──────────────────────────────────────────────────────────

test "EntryType.fromU16 - known types" {
    try std.testing.expectEqual(EntryType.Empty, try EntryType.fromU16(0));
    try std.testing.expectEqual(EntryType.CompressedSignedBeaconBlock, try EntryType.fromU16(1));
    try std.testing.expectEqual(EntryType.CompressedBeaconState, try EntryType.fromU16(2));
    try std.testing.expectEqual(EntryType.Version, try EntryType.fromU16(0x3265));
    try std.testing.expectEqual(EntryType.SlotIndex, try EntryType.fromU16(0x3269));
}

test "EntryType.fromU16 - unknown type returns error" {
    try std.testing.expectError(error.UnknownEntryType, EntryType.fromU16(0xFFFF));
    try std.testing.expectError(error.UnknownEntryType, EntryType.fromU16(3));
    try std.testing.expectError(error.UnknownEntryType, EntryType.fromU16(42));
}

test "EntryType.toU16 - roundtrip" {
    inline for (std.meta.fields(EntryType)) |field| {
        const entry = @field(EntryType, field.name);
        try std.testing.expectEqual(entry, try EntryType.fromU16(entry.toU16()));
    }
}

test "EntryType - Version is 'e2' in ASCII" {
    // 'e' = 0x65, '2' = 0x32, little-endian u16 = 0x3265
    try std.testing.expectEqual(@as(u16, 0x3265), EntryType.Version.toU16());
}

test "EntryType - SlotIndex is 'i2' in ASCII" {
    // 'i' = 0x69, '2' = 0x32, little-endian u16 = 0x3269
    try std.testing.expectEqual(@as(u16, 0x3269), EntryType.SlotIndex.toU16());
}

test "SlotIndex.serialize - basic" {
    const allocator = std.testing.allocator;
    const offsets = try allocator.alloc(i64, 2);
    defer allocator.free(offsets);
    offsets[0] = 100;
    offsets[1] = 200;

    const index = SlotIndex{
        .start_slot = 8192,
        .offsets = offsets,
        .record_start = 0,
    };

    const serialized = try index.serialize(allocator);
    defer allocator.free(serialized);

    // Size: 2 offsets * 8 + 16 = 32 bytes
    try std.testing.expectEqual(@as(usize, 32), serialized.len);

    // First 8 bytes: start_slot (8192 = 0x2000 LE)
    try std.testing.expectEqual(@as(u64, 8192), std.mem.readInt(u64, serialized[0..8], .little));

    // Middle 16 bytes: offsets
    try std.testing.expectEqual(@as(i64, 100), std.mem.readInt(i64, serialized[8..16], .little));
    try std.testing.expectEqual(@as(i64, 200), std.mem.readInt(i64, serialized[16..24], .little));

    // Last 8 bytes: count (2)
    try std.testing.expectEqual(@as(u64, 2), std.mem.readInt(u64, serialized[24..32], .little));
}

test "version_record_bytes" {
    // Version record is: type=0x3265 ("e2"), length=0, reserved=0
    try std.testing.expectEqual(@as(u8, 0x65), version_record_bytes[0]); // 'e'
    try std.testing.expectEqual(@as(u8, 0x32), version_record_bytes[1]); // '2'
    // Rest should be zeros (length=0, reserved=0)
    for (version_record_bytes[2..]) |b| {
        try std.testing.expectEqual(@as(u8, 0), b);
    }
}
